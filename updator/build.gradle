apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'//improtant
apply plugin: 'com.github.dcendents.android-maven'//for javadoc-task
version = "1.1.1"
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}

//important
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

//important 自动生成pom.xml文件
version = "1.1.1"//发布的library的版本号

def siteUrl = 'https://github.com/ukiy2010/Updator'      // 项目的主页(可随意)
def gitUrl = 'https://github.com/ukiy2010/Updator'      // Git仓库的url(可随意)
group = "com.ukiy.module"    // 这里是groupId ,必须填写(建议填包名)

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            // Add your description here
            name 'The library is a commonly used update module'
            //项目描述
            url siteUrl
            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'ukiy2010'        //开发者基本信息
                    name 'ukiy2010'    //开发者基本信息
                    email 'ukiy2012@gmail.com'   //开发者基本信息
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }

        }
    }
}

//生成source.jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成javadocJar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//important读取 local.properties 文件里面的 bintray.user和bintray.apikey
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        group = group
        name = "Updator"//发布到JCenter上的项目名字，必须填写
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}